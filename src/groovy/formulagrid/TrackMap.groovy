package formulagrid

class TrackMap {

    TrackPoint[][] bitmap

    TrackMap() {}

    int getHeight() {
        return bitmap[0].length
    }

    int getWidth() {
        return bitmap.length
    }

    TrackMap(TrackPoint[][] bitmap) {
        this.bitmap = bitmap
    }

    TrackPoint getTrackPoint(int x, int y) {
        return getTrackPoint(new Point(x, y))
    }

    // TODO overload [] operator
    TrackPoint getTrackPoint(Point point) {
        checkArgumentIsNotNull('point', point)
        return bitmap[point.x][point.y]
    }

    void moveCarTo(Car car, Point position) {
        if (car.position) {
            getTrackPoint(car.position).car = null
        }

        getTrackPoint(position).car = car
        car.setPosition(position)
    }

    void moveCarTo(Car car, Path path) {
        if (car.position) {
            getTrackPoint(car.position).car = null
        }

        getTrackPoint(path.to).car = car
        car.followPath(path)
    }

    Character[][] getAsciiRepresentation() {
        Character[][] representation = new char[width][height]

        (0..width-1).each { int x ->
            (0..height-1).each { int y ->
                representation[x][y] = bitmap[x][y].asciiRepresentation
            }
        }

        return representation
    }

    List<Integer> getAllObstacleAsList() {
        List list = []

        height.times { y ->
            width.times { x ->
                list << (getTrackPoint(x, y).isObstacle() ? 1 : 0)
            }
        }

        return list
    }

    // TODO Mettre cette mÃ©thode en commun
    private void checkArgumentIsNotNull(String argName, def arg) {
        if (arg == null) {
            throw new IllegalArgumentException("Argument '$argName' must be not null.")
        }
    }
}
